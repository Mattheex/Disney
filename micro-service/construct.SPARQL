PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX test: <http://ns.inria.fr/sparql-micro-service/api#>
PREFIX data: <http://projet.fr/perso_data/>
PREFIX : <http://projet.fr/perso_schema/>

PREFIX film: <http://projet.fr/films#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
construct {
  
  ?comic a :Comic;
    :name ?apparition.
  
  ?concept a owl:Concept;
    owl:prefLabel ?name;
    :publisher ?publishers;
    :apparition ?comic;
    owl:seeAlso ?hero,?perso.
  ?hero a ?genderClass, ?heroe, :Character;
    :name ?name;
    :alias ?alias;
    :appearence ?app;
    :alter ?alter;
    :base ?base;
    :affiliation ?groups;
    :relatives ?relative;
    :hasImage ?image;
    :secretIdentity ?perso;
    owl:seeAlso ?concept.
  
  ?perso a ?genderClass, ?heroe, :Character;
    :name ?fullName;
    :occupation ?occupations;
    :placeOfBirth ?placeOfBirth;
    :appearence ?app;
    :heroIdentity ?hero;
    owl:seeAlso ?concept.
    
  ?app a :Apparence;
      :intelligence ?int;
      :strength ?strength;
      :speed ?speed;
      :durability ?dura;
      :power ?power;
      :combat ?combat;
      :race ?modifiedRace;
      :heightF ?heightF;
      :heightCm ?heightCm;
      :weightKg ?weightKG;
      :weightLb ?weightLB;
      :eyes ?eye;
      :hair ?hairs.
}
WHERE {
  {
    BIND("http://projet.fr/perso_data/" as ?per).
  	?person test:name ?name;
         test:id ?id;
         test:biography ?bio;
         test:appearance ?appearance;
         test:work ?work;
         test:connections ?connections;
         test:powerstats ?stats;
         test:image [test:url ?image].
    OPTIONAL{?bio test:aliases ?alias.FILTER(?alias !="-")}
    OPTIONAL{?bio test:alter-egos ?alter.FILTER(?alter !="No alter egos found.")}
    OPTIONAL{?bio test:place-of-birth ?placeOfBirth.FILTER(?placeOfBirth !="-")}
    OPTIONAL{?bio test:first-appearance ?apparition.FILTER(?apparition !="-").BIND(URI(CONCAT("http://projet.fr/perso_data/",REPLACE(?apparition, "[^a-zA-Z0-9]", ""))) AS ?comic) }
    OPTIONAL{?bio test:publisher ?publisher.FILTER(?publisher!="null"). BIND(URI(CONCAT("http://projet.fr/perso_data/",?publisher)) AS ?publishers)}
    OPTIONAL{?bio test:full-name ?fullName.FILTER(?fullName!="")}
    ?bio test:alignment ?alignment.
    ?appearance test:gender ?gender.
    OPTIONAL{?appearance test:race ?race.FILTER(?race!="null").BIND(IRI(CONCAT("http://projet.fr/perso_data/",?race)) AS ?modifiedRace)}
    OPTIONAL{?appearance test:height ?hf.FILTER(?hf!="-" && CONTAINS(?hf,"'")). BIND(xsd:float(REPLACE(?hf,"'",".")) AS ?heightF) }
    OPTIONAL{?appearance test:height ?hc.FILTER(STRENDS(?hc,"cm")). BIND(xsd:integer(STRBEFORE(?hc," cm")) AS ?heightCm) }
    OPTIONAL{?appearance test:weight ?wk. FILTER(STRENDS(?wk,"kg")&& ?wk!="- kg"). BIND(xsd:integer(STRBEFORE(?wk," kg")) AS ?weightKG)}
    OPTIONAL{?appearance test:weight ?wl. FILTER(STRENDS(?wl,"lb")&& ?wl!="- lb"). BIND(xsd:integer(STRBEFORE(?wl," lb")) AS ?weightLB)}
    OPTIONAL{?appearance test:eye-color ?eyes. FILTER(?eyes!="-"). BIND(URI(CONCAT("http://projet.fr/perso_data/",?eyes)) AS ?eye)}
    OPTIONAL{?appearance test:hair-color ?hair.FILTER(?hair!="-"). BIND(URI(CONCAT("http://projet.fr/perso_data/",?hair)) AS ?hairs)}
    OPTIONAL{?work test:occupation ?occupation.FILTER(?occupation!="-"). BIND(URI(CONCAT("http://projet.fr/perso_data/",?occupation)) AS ?occupations)}
    OPTIONAL{?work test:base ?base.FILTER(?base!="-")}
    OPTIONAL{?connections test:group-affiliation ?groups.FILTER(?groups!="-")}
    OPTIONAL{?connections test:relatives ?relatives.FILTER(?relatives!="-"). BIND(URI(CONCAT(?per,?relatives)) AS ?relative)}
    OPTIONAL{?stats test:intelligence ?i.FILTER(?i!="-").BIND(xsd:integer(?i) AS ?int)}
    OPTIONAL{?stats test:strength ?st.FILTER(?st!="-").BIND(xsd:integer(?st) AS ?strength)}
    OPTIONAL{?stats test:speed ?sp.FILTER(?sp!="-").BIND(xsd:integer(?sp) AS ?speed)}
    OPTIONAL{?stats test:durability ?d.FILTER(?d!="-").BIND(xsd:integer(?d) AS ?dura)}
    OPTIONAL{?stats test:power ?p.FILTER(?p!="-").BIND(xsd:integer(?p) AS ?power)}
    OPTIONAL{?stats test:combat ?c.FILTER(?c!="-").BIND(xsd:integer(?c) AS ?combat)}
    BIND(IF(?alignment="good",:Heroe,:Villain) as ?heroe)
    BIND(IF(?gender="Male",:Male,:Female) as ?genderClass).
  	BIND(IRI(CONCAT( "http://projet.fr/perso_data/C",?name)) as ?concept).
    BIND(IRI(CONCAT( "http://projet.fr/perso_data/P",?id)) as ?perso).
    BIND(IRI(CONCAT( "http://projet.fr/perso_data/H",?id)) as ?hero).
  	BIND(IRI(CONCAT( "http://projet.fr/perso_data/A",?id)) as ?app).


  }
}
